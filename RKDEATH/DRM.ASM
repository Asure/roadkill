***
*** FILE: DRM.ASM
*** DERBY RACE MOVE ROUTINES
***
*** JFL 10 SEP 92
*** JFL 11 SEP 92
*** JFL 14 SEP 92; HOLD ON & OFF TIMES; JUMP
*** JFL 29 SEP 92; NEW SCRATCH
*** JFL 02 OCT 92
*** JFL 07 OCT 92
*** JFL 13 OCT 92; VEC
*** JFL 03 NOV 92; APRI
*** JFL 10 JAN 93; MISSILE
*** JFL 17 JAN 93; NO MORE JUMP CODE
*** JFL 18 JAN 93; MT_ROCKET,MT_PATRIOT,MT_SMART,MT_GUIDED
*** JFL 12 MAR 93; SIMPLE MOVES
*** JFL 13 APR 93; USE LMINE COUNT
*** JFL 16 APR 93; ADJUSTMENTS
***
	.MLIB	"APMACS.LIB"
	.FILE	"DR.ASM"
	.TITLE	" <<< DERBY RACE -- MOVE ROUTINES >>>"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST
**
** COMPILE FLAGS
**

***********************************************************************
*								         *
* 	COPYRIGHT (C) 1992 MIDWAY MANUFACTURING COMPANY.		 *
* 		       ALL RIGHTS RESERVED.				 *
*								         *
*************************************************************************

*	GET THE SYSTEM STUFF
	.INCLUDE	"AP.H"
	.INCLUDE	"DRINP.I"
	.INCLUDE	"DR.I"
	.INCLUDE	"DRM.I"
	.INCLUDE	"DRVEC.I"
	.INCLUDE	"DRC.I"
	.INCLUDE	"DRMIS.I"
	.INCLUDE	"DRSND.I"
	.INCLUDE	"DRV.I"

** OTHERS USE
	.DEF	SPECMOVE	;DRM.ASM
	.DEF	INITSPECMOVE	;DRM.ASM
	.DEF	ENDMOVE		;DRM.ASM

** WE USE
	.REF	SCORESETTURBO	;DRS.ASM
	.REF	SCORESETMISL	;DRS.ASM
	.REF	SCORESETLMINE	;DRS.ASM
	.REF	DOTURBO		;DRA.ASM
	.REF	CARTHROWUP	;DRC.ASM
	.REF	MISLAUNCH	;DRMIS.ASM
	.REF	MISFFPROC	;DRMIS.ASM
	.REF	MAKESND		;DRSND.ASM
	.REF	ARENASND	;DRSND.ASM
	.REF	CREATELANDMINE	;DRMIS.ASM
	.REF	WAVEIRQS
	.REF	FLASHCARPAL	;DRA.ASM
	.REF	DROPICON	;DRACT.ASM

** WE USE -- RAM
	.IF DEBUG
	.REF	DEBUGMOVE	;DRRAM.ASM
	.REF	WINNER		;DRRAM.ASM
	.ENDIF ;DEBUG
	
	.TEXT
	.EVEN

**
** DEFINES
**
MTTURNSPIN	EQU	5		;KICKOUT SPIN MAGNITUDE
MMAXDUR		EQU	127-HOFFDUR	;MAX DURATION FOR A MOVE
TURBOBOOST	EQU	800H		;TURBO BOOST

**
** INITSPECMOVE
**
**  A8 CAROBJ
**
** JFL 10 SEP 92
** JFL 29 SEP 92; NEW SCRATCH
** JFL 12 MAR 93; SIMPLE MOVES
**
INITSPECMOVE
ENDMOVE
	RETS

**
** MOVETBL
**
** FWT FORMAT
**
** JFL 12 MAR 93
**  
MOVETBL				;FWT
	.LONG	0		;000
	.LONG	MTURBO		;001
	.LONG	MFRENCHFRY	;010
	.LONG	MMISSILE	;011
	.LONG	MREV		;100
	.LONG	MDROPICON	;101
	.LONG	MLMINE		;110
	.LONG	0 ;MSPECIAL	;111

**
** SPECMOVE
**
** HANDLE SPECIAL MOVE
**
** IN
**   A7 CD
**   A8 CAROBJ
**
** BREAKS: A0,A1,A2,A3,A4,A5,A6,A9,A10,A11
**
** JFL 10 SEP 92
** JFL 16 SEP 92; NEG MOVE TIME
** JFL 29 SEP 92; NEW SCRATCH
** JFL 30 OCT 92; BAIL ON NO CDINPDATA
** JFL 12 MAR 93; SIMPLIFIED
**
SPECMOVE
	MOVL	*A7(CDINPDATA),A0
	JRZ	SMX

	;FORM A WORD THAT REPRESENTS THE BUTTON STATE OF THE THREE BUTTONS
	MOVB	*A0(INPB0),A1
	MOVB	*A0(INPB1),A2
	MOVB	*A0(INPB2),A3
	SLL	32-4,A1
	SLL	32-4,A2
	SLL	32-4,A3
	SRL	32-12,A1
	SRL	32-8,A2
	SRL	32-4,A3
	OR	A2,A1
	OR	A3,A1

	;GET & PUT HISTORY
	MOVW	*A7(CDMOVEOLD),A4
	MOVW	*A7(CDMOVEOLDER),A5
	MOVW	*A7(CDMOVEOLDEST),A6
	MOVW	A1,*A7(CDMOVEOLD)
	MOVW	A4,*A7(CDMOVEOLDER)
	MOVW	A5,*A7(CDMOVEOLDEST)

	;WAIT FOR CODE TO STABALIZE
	CMP	A1,A4
	JRNE	SMX
	CMP	A1,A5
	JRNE	SMX
	CMP	A1,A6
	JREQ	SMX

	;TURN CODE INTO A JUMP
	SRL	8,A1
	SLL	32-1,A1
	SRL	4,A2
	SLL	32-1,A2
	SLL	32-1,A3
	SRL	32-3-5,A1
	SRL	32-2-5,A2
	SRL	32-1-5,A3
	OR	A2,A1
	OR	A3,A1
	ADDI	MOVETBL,A1
	MOVL	*A1,A14
	JRZ	SMX

	CALL	A14 ;IN:A7,A8 CD,OBJ
	
SMX
	RETS

**
** MREV
**
**  A7 CD
**  A8 CAROBJ
** 
** OK TO TRASH ALL A REGS BEFORE RETURNING
**
** JFL 10 SEP 92
** JFL 29 SEP 92; NEW SCRATCH
** JFL 25 FEB 93; REV IN AIR POSTPONED
**
MREV
	MOVB	*A7(CDFLAGS+B_CDFDRONE-7),A14
	JRN	MRNOSND

	;SOUND SHIFT
	MOVW	*A7(CDINPNUM),A14
	ADDI	SNDREVERSE1,A14
	CALLA	ARENASND ;IN:A14 SNDID

MRNOSND
	;POSTPONE SHIFT UNTIL THEY HIT GROUND
	MOVL	*A8(OYVAL),A14
	JRZ	MRONGND
	
	;SET TO DO REV ON HITTING GROUND
	SETF	1,0,0
	MOVK	1,A14
	MOVE	A14,*A7(CDFLAGS+B_CDFDOREV),0
	SETF	16,1,0
	JRUC	MRX
	
MRONGND
	;DO REV
	MOVW	*A7(CDFLAGS),A14	
	XORI	M_CDFREV,A14
	MOVW	A14,*A7(CDFLAGS)

MRX
	RETS

**
** TURBOWATCHER
**
** IN
**   A8 OBJ
**   A9 CD
**  A10 TURBOBOOSTTBL PTR
**
** JFL 09 OCT 92
** JFL 13 OCT 92
** JFL 22 DEC 92; CDTURBOBOOST
** JFL 18 FEB 93
**
TURBOWATCHER
	MOVE	A9,A7

	;GET NEXT TURBO BOOST
	MOVW	*A10+,A11
	JRZ	TWENDMOVE

	;CHECK THE FLAGS
	MOVB	*A7(CDFLAGS+B_CDFSTOPEXT-7),A14
	JRN	TWENDMOVE

	;DONT EFFECT WHEN ALT INPUT
	MOVB	*A7(CDFLAGS+B_CDFALTINP-7),A14
	JRN	TWENDMOVE

	;CHECK FOR IN AIR, CANCEL TURBO
	;MOVL	*A8(OYVEL),A4
	;JRNZ	TWENDMOVE

	.IF 0 ;NO CANCEL
	;CHECK FOR RELEASE OF BUTTON CANCEL TURBO
	MOVL	*A7(CDINPDATA),A2
	MOVW	*A2(INPB2),A3
	MOVW	*A7(CDB2),A14
	CMP	A3,A14
	JRNE	TWENDMOVE
	.ENDIF ;NO CANCEL

	MOVW	A11,*A7(CDTURBOBOOST)

TWNEXT
	;NEXT
	SLEEP	3
	JRUC	TURBOWATCHER

TWENDMOVE
	;ONE LESS EXTERNAL FORCE
	MOVW	*A7(CDCOUNTEXT),A14
	DEC	A14
	MOVW	A14,*A7(CDCOUNTEXT)
	LOCKON	N

	;ZERO THE BOOST
	CLR	A10
	MOVW	A10,*A7(CDTURBOBOOST)

	;END THE MOVE
	DIE

**
** TURBOBOOSTTBL
**
** JFL 09 OCT 92
** JFL 13 APR 93; 2*PEDALMAX
** JFL 16 APR 93
**
TURBOBOOSTTBL
	.WORD	3*PEDALMAX
	.WORD	3*PEDALMAX/2
	.WORD	2*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	1*PEDALMAX/2
	.WORD	0

**
** MTURBO
**
**  A7 CD
**  A8 CAROBJ
**
** OK TO TRASH ALL A REGS, EXCEPT A7,A8, BEFORE RETURNING
**
** JFL 07 OCT 92
** JFL 09 OCT 92
** JFL 19 JAN 93; FRACTIONAL TURBOS
** JFL 10 FEB 93; TURBO BONUS
** JFL 19 MAR 93; NO TURBO WHILE IN AIR PENALTY
**
MTURBO
	;DONT EFFECT WHEN ALT INPUT
	MOVW	*A7(CDFLAGS),A14
	BTST	B_CDFAIRPENALTY,A14
	JRNZ	MTABORT

	;DONT DO WHEN CAR IS DEAD
	BTST	B_CDFDEAD,A14
	JRNZ	MTABORT

	;DONT USE UP TURBOS
	BTST	B_CDFBONUSTURBO,A14
	JRNZ	MTUSED

	;USE TURBO
	MOVW	*A7(CDTURBOS),A3
	SRL	TURBOFRACSHIFT,A3
	JRZ	MTABORT
	TLOCKON	N
	DEC	A3
	MOVE	A3,A14
	SLL	TURBOFRACSHIFT,A14
	MOVW	A14,*A7(CDTURBOS)
	MOVE	A8,A2
	CALLA	SCORESETTURBO ;IN: A2,A3 OBJ,TURBOS
MTUSED

	;ADD GRAPHIC
	MOVB	*A7(CDFLAGS+B_CDFREV-7),A11
	CALLA	DOTURBO ;IN: A8,A11 OBJ,FWD/REV

	;FLASH CAR
	MOVW	*A7(CDPALTURBO),A2
	SLL	16,A2
	MOVK	2,A14
	MOVX	A14,A2		;PAL:COUNT
	MOVI	[16,8],A3	;TIMEON:TIMEOFF
	CALLA	FLASHCARPAL ;IN:A2,A3,A7,A8 PAL:COUNT,TIMEON:TIMEOFF,CD,OBJ

	;AGGRESSIVE
	MOVW	*A7(CDAGG),A14
	INC	A14
	MOVW	A14,*A7(CDAGG)

	;ONE MORE EXTERNAL FORCE
	MOVW	*A7(CDCOUNTEXT),A14
	INC	A14
	MOVW	A14,*A7(CDCOUNTEXT)

	;REMEMBER BUTTON COUNT FOR TURBOWATCHER
	MOVL	*A7(CDINPDATA),A2
	MOVW	*A2(INPB2),A3
	MOVW	A3,*A7(CDB2)

	;CUT SLIP DOWN
	MOVW	*A7(CDSLIPFACTOR),A14
	MOVE	A14,A2
	SRL	3,A2
	SUB	A2,A14
	JRNN	MTCUTSLIP
	CLR	A14
MTCUTSLIP
	MOVW	A14,*A7(CDSLIPFACTOR)

	;SET UP FOR PROCESS
	MOVE	A7,A9
	MOVI	TURBOBOOSTTBL,A10	
	CREATE	PID_DRAGONE,TURBOWATCHER
	MOVE	A9,A7
	
	RETS
MTABORT
	MOVB	*A7(CDFLAGS+B_CDFDRONE-7),A14
	JRN	MTANOSND

	;SOUND NO EFFECT
	MOVW	*A7(CDINPNUM),A14
	ADDI	SNDBUTTON1,A14
	CALLA	MAKESND ;IN:A14 SNDID

MTANOSND
	RETS


**
** MMISSILE -- SMART
**
**  A7 CD
**  A8 CAROBJ
**
** OK TO TRASH ALL A REGS, EXCEPT A7,A8, BEFORE RETURNING
**
** JFL 07 OCT 92
** JFL 09 OCT 92
** JFL 18 JAN 93
**
MMISSILE

	;NO LAUNCHING WHILE BEING TOSSED UP
	MOVW	*A7(CDFLAGS),A14
	BTST	B_CDFAIRPENALTY,A14
	JRNZ	MMABORT

	;DONT DO WHEN CAR IS DEAD
	BTST	B_CDFDEAD,A14
	JRNZ	MMABORT

	;USE MISSILE
	MOVW	*A7(CDMISSILES),A3
	JRZ	MMABORT
	TLOCKON	N
	DEC	A3
	MOVW	A3,*A7(CDMISSILES)
	MOVE	A8,A2
	CALLA	SCORESETMISL ;IN: A2,A3 OBJ,COUNT

	MOVK	MT_SMART,A2
	CALLA	MISLAUNCH ;IN:A2,A7,A8 MISTYPE,CD,OBJ 
			  ;OUT:C SET IF NONE LAUNCHED
	JRC	MMABORT	

	;AGGRESSIVE
	MOVW	*A7(CDAGG),A14
	ADDK	2,A14
	MOVW	A14,*A7(CDAGG)

	RETS
MMABORT
	MOVB	*A7(CDFLAGS+B_CDFDRONE-7),A14
	JRN	MMANOSND

	;SOUND NO EFFECT
	MOVW	*A7(CDINPNUM),A14
	ADDI	SNDBUTTON1,A14
	CALLA	MAKESND ;IN:A14 SNDID

MMANOSND
	RETS

**
** MDEFENSE
**
**  A7 CD
**  A8 CAROBJ
**
** OK TO TRASH ALL A REGS, EXCEPT A7,A8, BEFORE RETURNING
**
** JFL 07 OCT 92
** JFL 09 OCT 92
** JFL 18 JAN 93
** JFL 11 FEB 93
**
MDEFENSE

	;USE MISSILE
	MOVW	*A7(CDMISSILES),A3
	SUBK	1,A3
	JRN	MDABORT
	MOVW	A3,*A7(CDMISSILES)
	MOVE	A8,A2
	CALLA	SCORESETMISL ;IN: A2,A3 OBJ,COUNT

	MOVK	MT_DEFENSE,A2
	CALLA	MISLAUNCH ;IN:A2,A7,A8 MISTYPE,CD,OBJ 
			  ;OUT:C SET IF NONE LAUNCHED
	JRC	MDABORT	

	;AGGRESSIVE
	MOVW	*A7(CDAGG),A14
	ADDK	2,A14
	MOVW	A14,*A7(CDAGG)

	RETS
MDABORT
	MOVB	*A7(CDFLAGS+B_CDFDRONE-7),A14
	JRN	MDANOSND

	;SOUND NO EFFECT
	MOVW	*A7(CDINPNUM),A14
	ADDI	SNDBUTTON1,A14
	CALLA	MAKESND ;IN:A14 SNDID

MDANOSND
	RETS

**
** MFRENCHFRY
** FORWARD FIRING WEAPON
**
**  A7 CD
**  A8 CAROBJ
**
** OK TO TRASH ALL A REGS, EXCEPT A7,A8, BEFORE RETURNING
**
** JFL 14 MAR 93
**
MFRENCHFRY
	;NO LAUNCHING WHILE BEING TOSSED UP
	MOVW	*A7(CDFLAGS),A14
	BTST	B_CDFAIRPENALTY,A14
	JRNZ	MFFABORT

	;DONT DO WHEN CAR IS DEAD
	BTST	B_CDFDEAD,A14
	JRNZ	MFFABORT
	
	;AGGRESSIVE
	MOVW	*A7(CDAGG),A14
	ADDK	2,A14
	MOVW	A14,*A7(CDAGG)

	MOVE	A7,A9			;SAVE TO USE
	CREATE	PID_DRAGONE,MISFFPROC
	MOVE	A9,A7			;UNSAVE

MFFABORT
	RETS

**
** MLMINE
**
**  A7 CD
**  A8 CAROBJ
**
** OK TO TRASH ALL A REGS, EXCEPT A7,A8, BEFORE RETURNING
**
** JFL 07 OCT 92
** JFL 09 OCT 92
** JFL 18 JAN 93
** JFL 11 FEB 93
** JFL 24 FEB 93
** JFL 13 APR 93; USE LMINE COUNT
**
MLMINE

	;NO LAUNCHING WHILE BEING TOSSED UP
	MOVW	*A7(CDFLAGS),A14
	BTST	B_CDFAIRPENALTY,A14
	JRNZ	MLMABORT

	;DONT DO WHEN CAR IS DEAD
	BTST	B_CDFDEAD,A14
	JRNZ	MLMABORT

	;USE MISSILE
	MOVW	*A7(CDLMINES),A3
	SUBK	1,A3
	JRN	MLMABORT
	MOVW	A3,*A7(CDLMINES)
	MOVE	A8,A2
	CALLA	SCORESETLMINE ;IN: A2,A3 OBJ,COUNT

	;AGGRESSIVE
	MOVW	*A7(CDAGG),A14
	ADDK	2,A14
	MOVW	A14,*A7(CDAGG)

	CALLA	CREATELANDMINE ;IN:A7,A8 CD,OBJ SC:A0-A6,A9-A11

	RETS
MLMABORT
	MOVB	*A7(CDFLAGS+B_CDFDRONE-7),A14
	JRN	MLMANOSND

	;SOUND NO EFFECT
	MOVW	*A7(CDINPNUM),A14
	ADDI	SNDBUTTON1,A14
	CALLA	MAKESND ;IN:A14 SNDID

MLMANOSND
	RETS

**
** MSPECIAL
**
**  A7 CD
**  A8 CAROBJ
**
** OK TO TRASH ALL A REGS, EXCEPT A7,A8, BEFORE RETURNING
**
** JFL 15 MAR 93
**
MSPECIAL
	RETS

**
** MDROPICON
**
**  A7 CD
**  A8 CAROBJ
**
** OK TO TRASH ALL A REGS, EXCEPT A7,A8, BEFORE RETURNING
**
** JFL 15 MAR 93
**
MDROPICON
	;AGGRESSIVE
	MOVW	*A7(CDAGG),A14
	ADDK	2,A14
	MOVW	A14,*A7(CDAGG)

	;ONLY ALLOW THIS ONCE
	MOVW	*A7(CDSPECIAL),A14
	JRNZ	MDIX
	INC	A14
	MOVW	A14,*A7(CDSPECIAL)

	;GET Z:X POS
	MOVW	*A8(OZPOS),A3
	MOVW	*A8(OXPOS),A14
	SLL	16,A3
	MOVX	A14,A3

	;CREATE THE ICON
	CALLA	DROPICON ;IN:A3 Z:X OUT:A0 OBJ SC:B0-B1,B8,A1-A5

	;SET POS & VELS
	MOVI	ICONDROPYPOS,A14
	MOVW	A14,*A0(OYPOS)
	MOVI	ICONDROPYACC,A14
	MOVL	A14,*A0(OYACC)

MDIX
	RETS


	.END
* EOF
