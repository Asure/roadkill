***
*** FILE: DRI.ASM
*** DERBY RACE INIT ROUTINES & DATA
***
*** JFL 16 AUG 92; FROM DR.ASM SPLIT
*** JFL 18 AUG 92; MOTION VECTOR METHOD
*** JFL 09 SEP 92
*** JFL 13 SEP 92; SCALE FIX
*** JFL 17 SEP 92
*** JFL 22 SEP 92
*** JFL 23 SEP 92
*** JFL 29 SEP 92; NEW SCRATCH
*** JFL 01 OCT 92; ANISPAWN
*** JFL 13 OCT 92; VEC
*** JFL 18 OCT 92; DRA.ASM SPLIT OFF
*** JFL 26 OCT 92; GEARING
*** JFL 09 NOV 92
*** JFL 15 JAN 93; DRONE
*** JFL 17 FEB 93
*** JFL 12 APR 93; NO LAPS OR KILLS IN SCORE BOX
***
	.MLIB	"APMACS.LIB"
	.FILE	"DRI.ASM"
	.TITLE	"<<< DERBY RACE -- INIT ROUTINES >>>"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

**
** COMPILE FLAGS
**

***********************************************************************
*								         *
* 	COPYRIGHT (C) 1992 MIDWAY MANUFACTURING COMPANY.		 *
* 		       ALL RIGHTS RESERVED.				 *
*								         *
*************************************************************************

*	GET THE SYSTEM STUFF
	.INCLUDE	"AP.H"
	.INCLUDE	"DRINP.I"
	.INCLUDE	"DR.I"
	.INCLUDE	"DRM.I"
	.INCLUDE	"DRO.I"
	.INCLUDE	"DRT.I"
	.INCLUDE	"DRD.I"
	.INCLUDE	"DRP.I"
	.INCLUDE	"DRC.I"
	.INCLUDE	"DRPAL.I"
	.INCLUDE	"DRSND.I"
	.INCLUDE	"DRMIS.I"
	.INCLUDE	"DRFSHOW.I"
	.INCLUDE	"DRFGHOST.I"
	.INCLUDE	"DRFHIT.I" ;SHOWHITTEST FLAG
	.INCLUDE	"IMGTBLM2.GLO"
	.INCLUDE	"IMGTBLC1.GLO"
	.INCLUDE	"PLAYERDA.I"

** OTHERS USE
	.DEF	INITDRDATA	;DRI.ASM
	.DEF	MAKESHADMULTI	;DRI.ASM
	.DEF	CREATECAR	;DRI.ASM
	.DEF	PLAYERIZE	;DRI.ASM
	.DEF	CARCODETBL	;DRI.ASM
	.DEF	INITONETIME	;DRI.ASM
	.DEF	FEELRUNNER	;DRI.ASM
	.DEF	SYSTEM_P

** WE USE
	.REF	TWOCARSHIT	;DRH.ASM
	.REF	CARDATA		;DR.ASM
	.REF	CARDATAX	;DR.ASM
	.REF	DRONEDATA 	;DR.ASM
	.REF	DRONEDATAX	;DR.ASM
	.REF	DRDATA		;DR.ASM
	.REF	PLAYERCARINP	;DR.ASM
	.REF	HITLIST		;DR.ASM
	.REF	DRCLEAR		;DR.ASM
	.REF	DRCLEARX	;DR.ASM
	.REF	INITSPECMOVE	;DRM.ASM
	.REF	SCORESETMISL	;DRS.ASM
	.REF	SCORESETLMINE	;DRS.ASM
	.REF	SCORESETTURBO	;DRS.ASM
	.REF	SCORESETTIME	;DRS.ASM
	.REF	SCORESETDAMAGE	;DRS.ASM
	.REF	INITDISPLAYLISTS ;APD.ASM
	.REF	CKINSANI	;APD.ASM
	.REF	SETOBJSCALE	;DRH.ASM
	.REF	ARENATRACKINFO	;DR.ASM
	.REF	ADDSHADOW	;DR.ASM
	.REF	ASHITTEST	;DRA.ASM	
	.REF	GETINPDATA	;DRINP.ASM
	.REF	INSANI		;APD.ASM

	.REF	FOURIMGTBL	;DRFOUR.ASM
	.REF 	FOURHITFRONT	;DRAS.ASM
	.REF	FOURHITBACK	;DRAS.ASM
	.REF	FOURHITMID   	;DRAS.ASM
	.REF	BS_PLAYERDATA

	.REF	GAMERASE
	.REF	WAVEIRQS

	.REF	OBJINITBLANK	;DRAS.ASM
	.REF	OBJINITBG 	;DRAS.ASM
	.REF	OBJINITSPAWN	;DRAS.ASM
	.REF	OBJINITBLANKAO	;DRAS.ASM
	.REF	OBJINITMISSILE	;DRAS.ASM
	.REF	OBJINITAOVEL	;DRAS.ASM

** WE USE -- RAM
	.REF	LAPCHECKPOINTS	;DRRAM.ASM
	.REF	LAPSTOWIN	;DRRAM.ASM
	.REF	OBJINITRAM	;DRRAM.ASM
	.REF	OBJINITRAMAO	;DRRAM.ASM
	.REF	OBJINITRAMBG	;DRRAM.ASM
	.REF	OBJINITRAMAOVEL	;DRRAM.ASM
	.REF	OBJINITRAMSPAWN	;DRRAM.ASM
	.REF	OBJINITRAMMISSILE ;DRRAM.ASM
	.REF	DRG		;DRRAM.ASM
	.REF	MISPICKUP	;DRMIS.ASM
	.REF	LMINEPICKUP	;DRMIS.ASM
	.REF	PLAYERRECORDTBL	;DRRAM.ASM
	
	.IF DEBUG
	.REF	DEBUGOBJ	;DR.ASM
	.ENDIF ;DEBUG

**
** DEFINES
**
REGENRATE	EQU	46


	.TEXT
	.EVEN



***
*** INITS ----------------------------------------------------------------
***

**
** INITONETIME
**
** JFL 17 FEB 93
**
INITONETIME
	;SET DRONE LEVEL
	MOVK	DLEVELBEST,A14
	MOVL	A14,@DRG+DRGDRONELEVEL
	RETS

**
** INITDRDATA
** MAKE SURE OUR DERBY RACE DATA IS ZEROED BETWEEN RACES
**
** JFL 13 JUL 92
** JFL 22 JUL 92
** JFL 29 JUL 92
** JFL 16 FEB 93
**
INITDRDATA

	;CLEAR THE DERBY RACE DATA BLOCK
	CLR	A2			;CLEAR VALUE
	MOVI	DRCLEAR,A0     		;ADR
IDD1	MOVE	A2,*A0+,W		;CLEAR
	CMPI	DRCLEARX,A0
	JRLT	IDD1
	
	;CLEAR DRG
	MOVI	DRG,A0     		;ADR
	MOVI	DRGCLRX/16,A3
IDD2	MOVE	A2,*A0+,W		;CLEAR
	DSJ	A3,IDD2

	;
	;INIT LAP DATA
	;
	MOVL	@ARENATRACKINFO,A0
	MOVB	*A0(TILAPSTOWIN),A14
	MOVW	A14,@LAPSTOWIN
	MOVB	*A0(TILAPMASK),A14
	MOVB	A14,@LAPCHECKPOINTS

	RETS

	.DEF	INITOBJINITRAM
**
** INITOBJINITRAM
**
** JFL 18 OCT 92
**
INITOBJINITRAM	
	MOVI	OBJINITBG,A2
	MOVI	OBJINITRAMBG,A3
	CALLR	IORSUB
	
	MOVI	OBJINITSPAWN,A2
	MOVI	OBJINITRAMSPAWN,A3
	CALLR	IORSUB

	MOVI	OBJINITBLANKAO,A2
	MOVI	OBJINITRAMAO,A3
	CALLR	IORSUB

	MOVI	OBJINITMISSILE,A2
	MOVI	OBJINITRAMMISSILE,A3
	CALLR	IORSUB

	MOVI	OBJINITAOVEL,A2
	MOVI	OBJINITRAMAOVEL,A3
	CALLR	IORSUB

	MOVI	OBJINITBLANK,A2
	MOVI	OBJINITRAM,A3

IORSUB
	MOVK	COSIZE/32,A14
IOIRLOOP
	MOVL	*A2+,*A3+
	DSJS	A14,IOIRLOOP
	RETS

**
** INITONECARFORNEWRACE
**
** IN
**   A7 CD
**   A8 CAR
**
** JFL 02 FEB 93
** JFL 12 APR 93; NO LAPS OR KILLS IN SCORE BOX
** JFL 13 APR 93; LMINE COUNT
** JFL 16 APR 93; MAX TURBOS
**
INITONECARFORNEWRACE
	MMTM	SP,A1,A5

	;RESET LAP INTERNAL
	MOVK	2,A14			;FIRST CHECKPOINT THAT COUNTS
	MOVW	A14,*A7(CDLASTCHECKPT)
	CLR	A14
	MOVW	A14,*A7(CDLAPS)
	MOVW	A14,*A7(CDCHECKPOINTS)
	MOVW	A14,*A7(CDKILLS)

	;GET PLAYER NUM
	MOVW	*A8(OID),A1
	SLL	SL_POID,A1
	SRL	SR_POID,A1
	DEC	A1
	
	MOVI	PD_RECORDSIZE,A5
	MPYU	A1,A5
	ADDI	BS_PLAYERDATA,A5		;ADR OF PLY

	;SET TURBOS
	MOVW	*A5(PD_TURBOS),A14
	MOVW	A14,*A7(CDTURBOMAX)		;SET MAX TURBO
	MOVI	TURBOSINGLE,A3
	SETF	8,0,1
	MPYU	A14,A3				;TURN INTO FRACTIONAL THING
	SETF	32,1,1
	MOVW	A3,*A7(CDTURBOS)

	;SET LAPS TO WIN (NOT USED)
	MOVW	@LAPSTOWIN,A14
	JRNZ	ICFNRLAPSTYLE

	;FIGHT ARENA
	MOVK	3,A2
	MOVW	*A5(PD_MISLS),A2
	CALLA	MISPICKUP ;IN:A2,A7,A8 COUNT,CD,OBJ 
	MOVK	3,A2
	MOVW	*A5(PD_MINES),A2
	CALLA	LMINEPICKUP ;IN:A2,A7,A8 COUNT,CD,OBJ 

	JRUC	ICFNRANYSTYLE

ICFNRLAPSTYLE

	;LAP ARENA -- EARN THEM
	CLR	A14
	MOVW	A14,*A7(CDMISSILES)
	MOVW	A14,*A7(CDLMINES)

ICFNRANYSTYLE
	MOVE	A8,A2
	MOVW	*A7(CDTURBOS),A3
	SRL	TURBOFRACSHIFT,A3
	CALLA	SCORESETTURBO ;IN: A2,A3 OBJ,TURBOS
	
	;RESET DAMAGE & DAMAGE BAR
	MOVE	A8,A2
	MOVW	*A7(CDDAMAGE),A3
	CALLA	SCORESETDAMAGE ;IN:A2,A3 OBJ,DAMAGE

	;RESET MISL SCOREBOX
	MOVE	A8,A2
	MOVW	*A7(CDMISSILES),A3
	CALLA	SCORESETMISL ;IN: A2,A3 OBJ,MISSILES

	;RESET LMINE SCOREBOX
	MOVE	A8,A2
	MOVW	*A7(CDLMINES),A3
	CALLA	SCORESETLMINE ;IN: A2,A3 OBJ,COUNT

	MMFM	SP,A1,A5
	RETS


PLNUM2OID	EQU	12	;SHIFT PLAYER NUM 0..2 TO OID (NOTE:1..3)
DRPMAC	.MACRO CARID,DRVID,STRTID,INPN
	.LONG	(:CARID:<<24)|(:DRVID:<<16)|(:STRTID:<<8)|(:INPN:)
	.ENDM

**
** CARCODETBL
**
** .LONG CARID:DRIVERID:STARTID:INPNUM
**
CARCODETBL
	DRPMAC	CARFOURGREEN,0,0,0
	DRPMAC	CARFOURBLUE,1,1,1
	DRPMAC	CARFOURRED,2,2,2


**
** PLAYERIZE
** TURN A DRONE CAR INTO A PLAYER CAR
**
** IN
**   A8 PLAYERNUM
** SCRATCH: A0-A11
** 
** JFL 16 FEB 93
**
PLAYERIZE
	;TURN PLNUM INTO OID TO LOOK FOR
	MOVE	A8,A2
	INC	A2			;1..3
	SLL	PLNUM2OID,A2		;SHIFT INTO PLACE
	
   	;FIND CAR TO TAKE OVER
	MOVL	@SUPPLSTS+SUPL_CAR,A0
	JRZ	PLZNO
PLZLOOP
	;CHECK IF OID IS CLOSE ENOUGH
	MOVW	*A0(OID),A14
	ANDI	MASK_PLAYER,A14
	CMP	A2,A14
	JREQ	PLZBODYSNATCH		;TAKE OVER

	MOVL	*A0(OSLINK),A0
	JRNZ	PLZLOOP

PLZNO
	;
	;THERE IS NO OBJ TO TAKE OVER, CREATE ONE!
	;
	MOVE	A8,A14
	SLL	5,A14
	ADDI	CARCODETBL,A14
	MOVL	*A14,A9
	MMTM	SP,A8
	CALLR	CREATECAR ;IN:A9 CARID:DRIVERID:STARTID:INPNUM 
			  ;OUT:A7,A8 CD,OBJ SC:A0-A11
	MOVE	A8,A0
	MMFM	SP,A8
	
PLZBODYSNATCH
	;
	;TAKE OVER OBJ!
	;

	GETST	A10
	DINT

	MOVL	*A0(OCAR),A7

	MOVE	A8,A2
	CALLA	GETINPDATA ;IN:A2 INPNUM OUT:A3
	MOVL	A3,*A7(CDINPDATA)
	
	;CLEAR DRONE FLAG
	CLR	A14
	MOVL	A14,*A7(CDDRONE)
	SETF	1,0,0
	MOVE	SP,*A7(CDFLAGS+B_CDFDRONE),0
	SETF	16,1,0
	
	PUTST	A10

	;ONE MORE PLAYER ONE LESS DRONE ALIVE
	MOVI	DRG,A2
	MOVW	*A2(DRGALIVEDRONES),A14
	DEC	A14
	MOVW	A14,*A2(DRGALIVEDRONES)
	MOVW	*A2(DRGALIVEPLAYERS),A14
	INC	A14
	MOVW	A14,*A2(DRGALIVEPLAYERS)

	RETS

**
** CAR INIT DATA
**
** CIDREGENERATE
** EVERY 8TH FRAME THE VALUE IS ADDED INTO CDTURBOS
** 100H IS CONSIDERED A WHOLE TURBO, SO IF WE WANT TO REGENERATE TURBOS
** ONE EVERY SECOND (8/55 * VALUE) = 37 FOR 100H FOR ONE SECOND
** 
** JFL 09 SEP 92
** JFL 11 NOV 92; HIT MASK
** JFL 19 JAN 93; REGENERATION
** JFL 04 FEB 93; MISLPAL
** JFL 20 MAR 93; DEAD PAL
**
CIDIMG		EQU	000H	;UHL IMG TBL
CIDXXX2		EQU	020H	;UHW
CIDREGENERATE	EQU	030H	;UHW REGENERATION SPEED (FOR TURBO...)
CIDDEADPAL	EQU	040H	;UHL
CIDOFLAGS	EQU	060H	;UHW OFLAGS
CIDMISLPAL	EQU	070H	;UHL MISL PAL
CIDMISLONPAL	EQU	090H	;UHL MISL ON CAR PAL
CIDXXX3		EQU	0B0H	;UHW
CIDCDFLAGS	EQU	0C0H	;UHW CDFLAGS
CIDXXX7		EQU	0D0H	;UHL
CIDINP		EQU	0F0H	;UHL INP HANDLER
CIDFEEL		EQU	110H	;UHL HANDLING
CIDDAMPARTS	EQU	130H	;UHL INIT
CIDPALTURBO	EQU	150H	;UHL FLASH PALETTE -- TURBO
CIDDAMAGE	EQU	170H	;UHW TOTAL DAMAGE
CIDHITMASK	EQU	180H	;UHL HIT MASK DATA
CIDFIRSTPAL	EQU	1A0H	;UHL CAR PALETTE
CIDPALICON	EQU	1C0H	;UHL FLASH PALETTE -- ICON PICKUP
CIDHITFRONT	EQU	1E0H	;UHL FRONT HIT ANIMS
CIDHITMID	EQU	200H	;UHL MID HIT ANIMS
CIDHITBACK	EQU	220H	;UHL REAR HIT ANIMS
CIDSIZE		EQU	240H	; SIZE


**
** RESETCAR
**
** IN
**   A0 OBJ
**   A1 CID
**   A7 CD
**   A9 CDSI
**
** JFL 26 OCT 92
** JFL 11 NOV 92; HIT MASK
** JFL 16 FEB 93
** JFL 12 MAR 93; CDPALICON & TURBO
**
RESETCAR
	MMTM	SP,A8

	;
	;SET STARTING POSITION & DIRECTION
	;
	MOVE	A9,A3
	ZEXT	A3,W
	SRL	8,A3
	.IF DEBUG
	CMPI	TISMAX,A3
	TLOCKON	HS
	.ENDIF ;DEBUG
	MOVI	TISSIZE,A14
	MPYU	A14,A3
	ADDI	TISTART,A3
	MOVL	@ARENATRACKINFO,A14
	ADD	A14,A3
	MOVW	*A3(TISY),*A0(OZPOS)
	MOVW	*A3(TISX),*A0(OXPOS)
	MOVB	*A3(TISDIR),A14
	MOVW	A14,*A7(CDWHEELOFFSET)

	MOVK	1,A14
	MOVW	A14,*A0(OTBLOFF)

	;
	;SET OBJ STUFF
	;
	MOVL	*A1(CIDIMG),*A0(OIMGTBL)

	;
	;SET CD STUFF
	;
	MOVW	*A1(CIDREGENERATE),*A7(CDREGENERATE)
	MOVW	*A1(CIDCDFLAGS),A14
	ORI	M_CDFDRONE,A14
	MOVW	A14,*A7(CDFLAGS)
	MOVL	*A1(CIDINP),*A7(CDINPHANDLER)
	MOVL	*A1(CIDINP),*A7(CDINPNORM)
	MOVL	*A1(CIDFEEL),*A7(CDFEEL)
	MOVL	*A1(CIDDAMPARTS),*A7(CDDAMPARTS)
	MOVL	*A1(CIDHITMASK),*A7(CDHITMASK)
	MOVW	*A1(CIDDAMAGE),*A7(CDDAMAGE)
	MOVI	TILTVALFLAT,A14
	MOVL	A14,*A7(CDLASTTILT)
	MOVL	*A1(CIDHITFRONT),*A7(CDHITFRONT)
	MOVL	*A1(CIDHITMID),*A7(CDHITMID)
	MOVL	*A1(CIDHITBACK),*A7(CDHITBACK)
	MOVK	4,A14
	MOVW	A14,*A7(CDSASTIME)

	.IF DEBUG
	MOVW	*A7(CDREGENERATE),A14
	CMPI	TURBOSINGLE,A14
	TLOCKON	HS ;CANT BE BIGGER
	.ENDIF ;DEBUG

	;
	;DRIVER ID STUFF
	;
	MOVE	A9,A14
	SLL	8,A14
	SRL	32-8,A14
	MOVW	A14,*A7(CDDRIVERID)

	;SET SOUNDLIST
	SLL	5,A14		;SIZEOF SOUNDLISTTBL
	ADDI	SOUNDLISTTBL,A14
	.IF DEBUG
	CMPI	SOUNDLISTTBLX,A14
	TLOCKON	HS
	.ENDIF ;DEBUG
	MOVL	*A14(0),*A7(CDSOUNDLIST)

	;
	;INPNUM STUFF
	;
	MOVE	A9,A2
	SLL	32-8,A2
	SRL	32-8,A2
	MOVW	A2,*A7(CDINPNUM)
	ADDK	DRPLAYERS,A2		;SKIP PLAYER INPUTS
	CALLA	GETINPDATA ;IN:A2 NUM OUT:A3 DATA
	MOVL	A3,*A7(CDINPDATA)
	SUBK	DRPLAYERS,A2

	;SET DRONE BLOCK
	MOVI	DDSIZE,A3
	MPYU	A2,A3
	ADDI	DRONEDATA,A3
	.IF DEBUG	
	CMPI	DRONEDATAX,A3
	TLOCKON	HS
	.ENDIF ;DEBUG
	MOVL	A3,*A7(CDDRONE)
	
	;CHECK IF THIS IS A WOULD BE PLAYER & SET OID IF IT IS
	CMPI	DRPLAYERS,A2
	JRGE	RCNOTP
	MOVE	A2,A14
	SLL	PLNUM2OID,A14
	ADDI	OID_P1,A14
	MOVW	A14,*A0(OID)
RCNOTP
	MOVE	A0,A8		;SAVE

	;
	;MAKE SURE OTHER PALS ARE LOADED IN
	;
	MOVL	*A1(CIDPALICON),A0
	CALLA	GETFPAL ;IN:A0
	MOVW	A0,*A7(CDPALICON)
	MOVL	*A1(CIDPALTURBO),A0
	CALLA	GETFPAL ;IN:A0
	MOVW	A0,*A7(CDPALTURBO)
	MOVL	*A1(CIDMISLPAL),A0
	CALLA	GETFPAL ;IN:A0
	MOVW	A0,*A7(CDMISLPAL)
	MOVL	*A1(CIDMISLONPAL),A0
	CALLA	GETFPAL ;IN:A0
	MOVW	A0,*A7(CDMISLONPAL)
	MOVL	*A1(CIDDEADPAL),A0
	CALLA	GETFPAL ;IN:A0
	MOVW	A0,*A7(CDDEADPAL)

	;
	;INIT THE MOVE SYSTEM
	;
	CALLA	INITSPECMOVE ;IN: A8 OBJ

	;SET SCALE
	CALLA	SETOBJSCALE ;IN: A8 OBJ

	MOVE	A8,A0
	MMFM	SP,A8
	RETS

**
** CREATECAR
** CREATES CAR AS A DRONE
**
** IN
**   A9 CARID:DRIVERID:STARTINGID:INPNUM
** OUT
**   A7 CD
**   A8 CAR
**
** TRASHES A0-A11
** 
** JFL 12 JUL 92
** JFL 14 JUL 92
** JFL 17 JUL 92
** JFL 21 JUL 92
** JFL 23 JUL 92
** JFL 03 AUG 92
** JFL 04 AUG 92
** JFL 05 AUG 92
** JFL 13 AUG 92
** JFL 14 AUG 92
** JFL 16 AUG 92
** JFL 13 SEP 92
** JFL 24 SEP 92; MORE HACKS
** JFL 18 OCT 92; REDONE AS CREATE CAR
** JFL 17 NOV 92
** JFL 15 JAN 93; PASS IN ONLY CAR NUMBER
** JFL 16 FEB 93
** JFL 25 MAR 93; SINGLE-PART CAR
**
CREATECAR
	;FIGURE CD
	MOVE	A9,A2
	SLL	32-8,A2
	SRL	32-8,A2
	MOVI	CDSIZE,A7
	MPYU	A2,A7
	ADDI	CARDATA,A7
	.IF DEBUG
	CMPI	CARDATAX,A7
	TLOCKON	HS
	.ENDIF ;DEBUG

	;ZERO CD BLOCK
	MOVI	CDSIZE/16,A3
	MOVE	A7,A2
	CLR	A14
CCZCD
	MOVW	A14,*A2+
	DSJ	A3,CCZCD

	;GET CAR INIT DATA POINTER
	MOVE	A9,A1
	SRL	32-8,A1
	MOVI	CIDSIZE,A2	
	MPYU	A2,A1
	ADDI	CARDEFS,A1
	.IF DEBUG
	CMPI	CARDEFSX,A1
	TLOCKON	HS
	.ENDIF ;DEBUG

	;SET UP TO CREATE THE CHASIS (AS HEAD OF THE OBJ)
	CALLR	INITOBJINITRAM
	MOVI	OBJINITRAM,A5

	;GET THE IHDR FOR THE ZERO ROTATION CHASIS
	MOVL	*A1(CIDIMG),A2		;PTR TO IMG TBL
	MOVL	*A2,A2			;PTR TO IHDR
	MOVL	A2,*A5(COIMG)
	
	;TYPE OF SCALING
	MOVI	STYPZ,A14
	MOVW	A14,*A5(COSCALETYPE)

	;SET UP MISC	
	MOVI	OID_DRONE,A14
	MOVW	A14,*A5(COID)
	MOVW	*A1(CIDOFLAGS),*A5(COFLAGS)
	MOVI	DMAWNZ,A14
	MOVW	A14,*A5(COCTRL)	
	
	CALLA	CREATE_OBJ ;IN: A5 CREATE_OBJ INIT BLOCK
	JRZ	CCX

	;SET UP WITH DESIRED PAL
	MMTM	SP,A0
	MOVL	*A1(CIDFIRSTPAL),A0
	CALLA	GETFPAL ;IN:A0 ADR OF PAL
	MOVE	A0,A14
	MMFM	SP,A0
	MOVW	A14,*A0(OPAL)

	;FILL IN THE HEAD OBJECT DATA
	MOVL	A7,*A0(OCAR)
	CALLR	RESETCAR ;IN: A0,A1,A7,A9 OBJ,CID,CD,CDSI

	;ADD A DMA SHADOW OBJ
	MOVE	A0,A8
	CALLA	ADDSHADOW ;IN A2-A3,A8 SC,OBJ

	;INSERT THE OBJECT
	CALLA	INSERT_OBJ ;IN: A8 OBJ (MULTIPART HEAD OR PART)

	;ONE MORE CAR ALIVE	
	MOVI	DRG,A2
	MOVW	*A2(DRGALIVE),A14
	INC	A14
	MOVW	A14,*A2(DRGALIVE)

	;ONE MORE DRONE ALIVE
	MOVW	*A2(DRGALIVEDRONES),A14
	INC	A14
	MOVW	A14,*A2(DRGALIVEDRONES)

	CALLR	INITONECARFORNEWRACE ;IN:A2,A7,A8 SC,CD,OBJ

CCX
	RETS


**
** MAKESHADMULTI
**
** IN
**   A2	SHAD IMG
**   A3-A5 SCRATCH
**   A8 OBJ
** OUT
**   A0 SHADOBJ
**
** JFL 29 DEC 92
** JFL 24 FEB 93; FORCED OID
**
MAKESHADMULTI
	
	;CREATE AN OBJ
	MOVI	OBJINITRAM,A5
	MOVL	A2,*A5(COIMG)
	CLR	A14 ;OID_JUNK
	MOVW	A14,*A5(COID)

	CALLA	CREATE_OBJ ;IN: A5 CREATE_OBJ INIT BLOCK
	JRZ	MSMX
	
	;FILL IN
	MOVL	*A8(OXVAL),*A0(OXVAL)
	MOVI	SHADZOFF,A3
	;MOVW	A3,*A0(OZOFF) -- NOT NECCESSARY FOR MULTIPART FG OBJS
	SLL	16,A3
	MOVL	*A8(OZVAL),A14
	SUB	A3,A14
	MOVL	A14,*A0(OZVAL)
	MOVI	SHADOWPART,A14
	MOVW	A14,*A0(OPARTID)

	;FREE PAL OBJ WAS CREATED WITH
	MOVE	A0,A5			;SAVE
	MOVW	*A0(OPAL),A0
	CALLA	FREEPAL ;IN A0 PALNUM
	MOVE	A5,A0			;RESTORE
	
	;BUMP UP SYSTEM PAL
	CLR	A5 ;SPAL
	CALLA	INC_PALCNT ;IN A5 PALNUM
	
	MOVI	SHADPALCONST,A14
	MOVL	A14,*A0(OPAL)		;SETUP FOR CONST COLOR

	;SET CONSTANT COLOR CTRL
	MOVW	*A0(OCTRL),A14		;GET CTRL
	ANDI	DMABPP,A14		;KEEP BITS PER PIXEL
	ORI	DMACNZ,A14		;SET WRITE CONST
	MOVW	A14,*A0(OCTRL)

	;LINK IN AT END OF MULTIPART LIST
	MOVE	A8,A3
MSMLOOP
	;FIND LAST PART IN ALREADY
	MOVE	A3,A2
	MOVL	*A3(OPARTS),A3
	JRNZ	MSMLOOP

	;LINK IN
	MOVL	A8,*A8(OPART1)		;MAKE SURE HEAD KNOWS ITS MULTI
	MOVL	A8,*A0(OPART1)		;LINK BACK TO HEAD FROM THIS PART
	MOVL	A0,*A2(OPARTS)		;LINK FROM PREV PART TO US

	MOVL	A0,*A8(OSHADIMG)	;FROM OBJ TO US
	MOVL	A8,*A0(OSHADIMG)	;BACK TO OBJ
	
MSMX
	RETS


MISZOFF		EQU	-8

**
** MAKEMISSILEMULTI
**
** IN
**   A2-A5 SCRATCH
**   A7 CD
**   A8 OBJ
**
** JFL 29 DEC 92
**
MAKEMISSILEMULTI
	
	;CREATE AN OBJ
	MOVI	OBJINITRAM,A5
	MOVI	BOX,A2		;ANY OBJ
	MOVL	A2,*A5(COIMG)

	;SET W/MISSILE PART OFF
	SETF	1,0,0
	MOVE	SP,*A5(COCTRL+B_INUSE),0
	SETF	16,1,0

	CALLA	CREATE_OBJ ;IN: A5 CREATE_OBJ INIT BLOCK
	JRZ	MMMX
	
	;SET UP WITH DESIRED PAL
	MOVW	*A7(CDMISLONPAL),*A0(OPAL)

	;FILL IN
	MOVL	*A8(OXVAL),*A0(OXVAL)
	MOVI	MISZOFF,A3
	;MOVW	A3,*A0(OZOFF) -- NOT NECCESSARY FOR MULTIPART FG OBJS
	SLL	16,A3
	MOVL	*A8(OZVAL),A14
	SUB	A3,A14
	MOVL	A14,*A0(OZVAL)
	MOVI	MISSILEPART,A14
	MOVW	A14,*A0(OPARTID)

	MOVI	STYPEMISSILE,A14
	MOVW	A14,*A0(OSCALETYPE)

	;LINK IN AT END OF MULTIPART LIST
	MOVE	A8,A3
MMMLOOP
	;FIND LAST PART IN ALREADY
	MOVE	A3,A2
	MOVL	*A3(OPARTS),A3
	JRNZ	MMMLOOP

	;LINK IN
	MOVL	A8,*A8(OPART1)		;MAKE SURE
	MOVL	A0,*A2(OPARTS)
	MOVL	A8,*A0(OPART1)
	
MMMX
	RETS


SPEEDGEARING	EQU	0

**
** CAR HANDLING DATA
**
** --- GEARS & MAX SPEEDS ---
**
** NOTE: SEE DRINP.I FOR MIN AND MAX PEDAL VALUES... PEDALMAX = 235
**
** WHEN CAR SPEED EXCEEDS A MAX GEAR SPEED, IT SHIFTS TO NEXT GEAR
** WHEN CAR IS IN REVERSE, IT USES NEGATIVE VALUES OF THE MAX SPEED
** TO DECIDE WHEN TO SHIFT TO NEXT GEAR
** THE FORMULA FOR VEL IS V = V + ((PEDAL * PEDMUL) - V) * ACCMUL
** THE PEDAL IS IN THE RANGE OF 0..235 SO THE ABSOLUTE MAX SPEED IN A GEAR
** IS 235 * PEDMUL, THE ACCMUL DETERMINES HOW FAST THE CAR WILL COME TO
** MAX SPEED, I.E. IF ACCMUL IS 1/2 IT WILL TAKE 2 ITERATIONS, 1/8 WILL TAKE
** 8 ITERATIONS...
**
** PEDALMUL UNSIGNED 4 INTEGER BITS, 4 FRACTIONAL BITS <- PEDMULFRACBITS
** SO MAX VALUE IS 127 * 235 = 29845 >> 4 = 1865 = 749H AROUND 7 1/4 PIXELS
** PEDMUL * PEDALMAX MUST EXCEED MAX GEAR SPEED OR YOULL BE STUCK IN GEAR
** 
** FRACTION BITS: 1=.0625 2=.125 4=.25 8=.5 3=.1875 ETC
**
** FURTHER THE MAX NEG VEL & MAX POS VEL IS USED TO CLAMP CARS VELS.
** CURRENT MAX CAR TRAVEL IS 8 PIXELS PER FRAME... THIS WORKS OUT
** (AFTER THE VELSHIFT MULTIPLIER) TO 2048 AS MAX SPEED -- SINCE WE MAY WANT
** TO ALLOW A BUMP OR TURBO TO STILL WORK, KEEP SPEED TO 7 PIXELS PER FRAME
** OR 1792 OR LESS.
**
** CAR VELOCITIES SHIFTED BY VELSHIFT TO TURN THEM INTO PIXEL SPEED -- DR.I
** NOW VELSHIFT = 8 SO A VALUE OF 800H TURNS INTO 80000H OR 8 SCREEN PIXELS
**
** --- TURBO ---
**
** TURBO IS DEFINED IN DRM.ASM
** THE VALUES FROM THE TABLE TURBOBOOSTTBL ARE ADDED AS IF THEY WERE PEDAL
** PRESSES -- CURRENTLY THIS IS 2*PEDALMAX
**
** --- AREA STRENGTHS & DAMAGE VALUES ---
**
** EACH AREA HAS A STRENGTH AND A DAMGIVE VALUE.
** THE ORDERING IS 7:6:5:4:3:2:1:0 AND EACH VALUE CAN BE 0..F
** STRENGTHS ARE USED AS PART OF DETERMINING WHO WINS A COLLISION, I.E.
** HOW STRONG THE CAR IS AT THAT PART, AND DAMGIVE DETERMINES HOW MUCH
** DAMAGE THE WINNER OF THE COLLISION GIVES THE LOSER.
**
** --- WHEEL RESPONSE ---
**
** THIS VALUE IS USED TO SHIFT THE WHEEL POSITION INTO A DIRECTION THAT
** THE CAR IS FACING... A VALUE OF 20 MEANS SOMETHING LIKE 1 AND 1/2 TURNS
** SPINS THE CAR IN A FULL CIRCLE, A VALUE OF 19 MEANS SOMETHING LIKE 3
** TURNS SPINS THE CAR IN A FULL CIRCLE.
**
** --- WHEEL ANTI SLIP ---
**
** THIS MAY TURN INTO A WEIGHT FIELD
** THIS IS KIND OF THE OPPOSITE OF WHEEL RESPONSE, IT IS SUBTRACTED FROM
** THE WHEEL SPIN MAGNITUDE BEFORE TESTING FOR SLIPPING ON TRACK
**
** JFL 25 SEP 92; COMMENTS ADDED
** JFL 26 OCT 92; FIDDLED WITH GEARING
** JFL 04 DEC 92
** JFL 22 DEC 92
** JFL 13 APR 93; SLOW DOWN
**
FEELFOUR
	;NOTES:
	;MAX SPEED IS 1280, MAX GEARING SPEED (W/O TURBO) IS 1088
	;X * PEDALMAX = 1088
	;GEAR 0
	.WORD	090H		;MAX GEAR SPEED
	.BYTE	48H,-2		;PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)
	;GEAR 1
	.WORD	0A0H		;MAX GEAR SPEED
	.BYTE	2AH,-4		;PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)
	;GEAR 2	-- MULTIPLIERS ALSO USED FOR TURBO
	.WORD	0C0H		;MAX GEAR SPEED
	.BYTE	30H,-4		;PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)
	;GEAR 3	-- MULTIPLIERS SHOULDNT MAX OUT SPEED TO ALLOW FOR TURBOBOOST
	.WORD	32767		;MAX GEAR SPEED	-- NEVER GETS TOO FAST
	.BYTE	34H,-5		;5.5 PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)

	.WORD	-700H		;MAX NEG VEL
	.WORD	700H		;MAX POS VEL
	.LONG	012211121H	;AREA STRENGTHS
	.LONG	066666666H	;DAMGIVE VALUES
	.WORD	90		;WHEEL RESPONSE, BIGGER IS MORE RESPONSIVE	
	.WORD	90		;ANTISLIP, OPPOSITE OF ABOVE, BIG IS LESS SLIP


FEELRUNNER	
	;NOTES:
	;MAX SPEED IS 1280, MAX GEARING SPEED (W/O TURBO) IS 1088
	;X * PEDALMAX = 1088
	;GEAR 0
	.WORD	080H		;MAX GEAR SPEED
	.BYTE	38H,-2		;PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)
	;GEAR 1
	.WORD	090H		;MAX GEAR SPEED
	.BYTE	26H,-4		;PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)
	;GEAR 2	-- MULTIPLIERS ALSO USED FOR TURBO
	.WORD	0A0H		;MAX GEAR SPEED
	.BYTE	26H,-4		;PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)
	;GEAR 3	-- MULTIPLIERS SHOULDNT MAX OUT SPEED TO ALLOW FOR TURBOBOOST
	.WORD	32767		;MAX GEAR SPEED	-- NEVER GETS TOO FAST
	.BYTE	30H,-5		;5.5 PEDMUL (MPYU), ACCMUL (+ FOR SLL,- SRA)

	.WORD	-300H		;MAX NEG VEL
	.WORD	300H		;MAX POS VEL
	.LONG	012211121H	;AREA STRENGTHS
	.LONG	066666666H	;DAMGIVE VALUES
	.WORD	90		;WHEEL RESPONSE, BIGGER IS MORE RESPONSIVE	
	.WORD	90		;ANTISLIP, OPPOSITE OF ABOVE, BIG IS LESS SLIP

**
** SOUNDLISTTBL
**
** INDEX BY PLAYER: DRONE,GARTH,CHIP,DUDE,FONZ
**
** JFL 01 FEB 93
**
SOUNDLISTTBL
	.LONG	SOUNDLISTDRONE
	.LONG	SOUNDLISTGARTH
	.LONG	SOUNDLISTCHIP
	.LONG	SOUNDLISTDUDE
	.LONG	SOUNDLISTFONZ
SOUNDLISTTBLX

**
** SOUND LISTS
**
** .WORD GOTHITCOUNTMASK,GOTHITSNDID1,GOTHITID2..GOTHITIDN,
** .WORD HITOTHERCOUNTMASK,HITOTHERSNDID1...
**
** A ZERO IN SNDID INDICATES NO SOUND
** COUNTMASK MAY BE ZERO -- PICK FIRST ALL THE TIME
**
** JFL 20 NOV 92
** JFL 01 FEB 93
**
SOUNDLISTDRONE
	.WORD 00H ;COUNTMASK
	.WORD SNDHITDRONE1
	.WORD 00H ;COUNTMASK
	.WORD SNDDONKDRONE1
SOUNDLISTGARTH
	.WORD 07H ;COUNTMASK
	.WORD SNDHITGARTH1,SNDHITGARTH2,SNDHITGARTH3,SNDHITGARTH4
	.WORD SNDHITGARTH5,SNDHITGARTH6,SNDHITGARTH1,SNDHITGARTH2
	.WORD 07H ;COUNTMASK
	.WORD SNDDONKGARTH1,SNDDONKGARTH2,SNDDONKGARTH3,SNDDONKGARTH4
	.WORD SNDDONKGARTH5,SNDDONKGARTH1,SNDDONKGARTH2,SNDDONKGARTH3
SOUNDLISTCHIP
	.WORD 07H ;COUNTMASK
	.WORD SNDHITCHIP1,SNDHITCHIP2,SNDHITCHIP3,SNDHITCHIP4
	.WORD SNDHITCHIP5,SNDHITCHIP6,SNDHITCHIP7,SNDHITCHIP1
	.WORD 03H ;COUNTMASK
	.WORD SNDDONKCHIP1,SNDDONKCHIP2,SNDDONKCHIP3,SNDDONKCHIP4
SOUNDLISTDUDE
	.WORD 07H ;COUNTMASK
	.WORD SNDHITDUDE1,SNDHITDUDE2,SNDHITDUDE3,SNDHITDUDE4
	.WORD SNDHITDUDE5,SNDHITDUDE6,SNDHITDUDE1,SNDHITDUDE2
	.WORD 01H ;COUNTMASK
	.WORD SNDDONKDUDE1,SNDDONKDUDE2
SOUNDLISTFONZ	       
	.WORD 07H ;COUNTMASK
	.WORD SNDHITFONZ1,SNDHITFONZ2,SNDHITFONZ3,SNDHITFONZ4
	.WORD SNDHITFONZ5,SNDHITFONZ1,SNDHITFONZ2,SNDHITFONZ3
	.WORD 03H ;COUNTMASK
	.WORD SNDDONKFONZ1,SNDDONKFONZ2,SNDDONKFONZ3,SNDDONKFONZ1
	


**
** DATA
**
** SEE CID STRUCT ABOVE FOR DESCRIPTION OF FIELDS
**
** JFL 29 JUL 92
** JFL 03 AUG 92
** JFL 04 AUG 92
** JFL 07 AUG 92
** JFL 03 SEP 92; PALETTE
** JFL 09 SEP 92
** JFL 26 OCT 92
** JFL 19 JAN 93
** JFL 13 FEB 93
** JFL 12 MAR 93
**
CARDEFS
     	
	;
	;FOUR
	;
	.LONG	FOURIMGTBL		;IMAGE TABLE
 	.WORD	0			;XXX
	.WORD	REGENRATE		;REGENERATE
	.LONG	PU_GR_P			;DEADPAL
	.WORD	M_NOAUTOZ		;OFLAGS
	.LONG	MISLG_P			;MISLPAL
	.LONG	MISN_P			;MISLONPAL
	.WORD	0			;XXX
	.WORD	0			;CDFLAGS
	.LONG	0			;XXX
	.LONG	PLAYERCARINP		;INP HANDLER
	.LONG	FEELFOUR		;HANDLING
	.LONG	088888888H		;DAMPARTS INIT
	.LONG	PU_GR_P			;FLASH PAL TURBO
	.WORD	NODAMAGE		;LIFE
	.LONG	0			;XXX
	.LONG	PU_GR_P			;CAR PALETTE
	.LONG	PU_GR_P			;FLASH PAL ICON
	.LONG	FOURHITFRONT		;HITFRONT
	.LONG	FOURHITMID		;HITMID
	.LONG	FOURHITBACK		;HITBACK

	;
	;FOUR
	;
	.LONG	FOURIMGTBL		;IMAGE TABLE
 	.WORD	0			;XXX
	.WORD	REGENRATE		;REGENERATE
	.LONG	PU_B_P			;DEADPAL
	.WORD	M_NOAUTOZ		;OFLAGS
	.LONG	MISLB_P			;MISLPAL
	.LONG	MISN_P			;MISLONPAL
	.WORD	0			;XXX
	.WORD	0			;CDFLAGS
	.LONG	0			;XXX
	.LONG	PLAYERCARINP		;INP HANDLER
	.LONG	FEELFOUR		;HANDLING
	.LONG	088888888H		;DAMPARTS INIT
	.LONG	PU_B_P			;FLASH PAL TURBO
	.WORD	NODAMAGE		;LIFE
	.LONG	0			;XXX
	.LONG	PU_B_P			;CAR PALETTE
	.LONG	PU_Y_P			;FLASH PAL ICON
	.LONG	FOURHITFRONT		;HITFRONT
	.LONG	FOURHITMID		;HITMID
	.LONG	FOURHITBACK		;HITBACK

	;
	;FOUR
	;
	.LONG	FOURIMGTBL		;IMAGE TABLE
 	.WORD	0			;XXX
	.WORD	REGENRATE		;REGENERATE
	.LONG	PU_R_P			;DEADPAL
	.WORD	M_NOAUTOZ		;OFLAGS
	.LONG	MISLR_P			;MISLPAL
	.LONG	MISN_P			;MISLONPAL
	.WORD	0			;XXX
	.WORD	0			;CDFLAGS
	.LONG	0			;XXX
	.LONG	PLAYERCARINP		;INP HANDLER
	.LONG	FEELFOUR		;HANDLING
	.LONG	088888888H		;DAMPARTS INIT
	.LONG	PU_R_P			;FLASH PAL TURBO
	.WORD	NODAMAGE		;LIFE
	.LONG	0			;XXX
	.LONG	PU_R_P			;CAR PALETTE
	.LONG	PU_Y_P			;FLASH PAL ICON
	.LONG	FOURHITFRONT		;HITFRONT
	.LONG	FOURHITMID		;HITMID
	.LONG	FOURHITBACK		;HITBACK

	;
	;FOUR
	;
	.LONG	FOURIMGTBL		;IMAGE TABLE
 	.WORD	0			;XXX
	.WORD	REGENRATE		;REGENERATE
	.LONG	PU_R_P			;DEADPAL
	.WORD	M_NOAUTOZ		;OFLAGS
	.LONG	MISLG_P			;MISLPAL
	.LONG	MISN_P			;MISLONPAL
	.WORD	0			;XXX
	.WORD	0			;CDFLAGS
	.LONG	0			;XXX
	.LONG	PLAYERCARINP		;INP HANDLER
	.LONG	FEELFOUR		;HANDLING
	.LONG	088888888H		;DAMPARTS INIT
	.LONG	PU_R_P			;FLASH PAL TURBO
	.WORD	NODAMAGE		;LIFE
	.LONG	0			;XXX
	.LONG	PU_R_P			;CAR PALETTE
	.LONG	PU_R_P			;FLASH PAL ICON
	.LONG	FOURHITFRONT		;HITFRONT
	.LONG	FOURHITMID		;HITMID
	.LONG	FOURHITBACK		;HITBACK
CARDEFSX

**
** SYSTEM_P
**
** SEE DRPAL.I
**
** JFL 20 NOV 92
** JFL 03 JAN 93; SHADOW
** JFL 11 JAN 93; WHITE
**
SYSTEM_P
	.word	7
	.word	00H,31*REDM,31*GREM,31*BLUM ;CLEAR,RED,GREEN,BLUE
	.WORD	31*GREM ;31*REDM+31*GREM ;YELLOW
	.WORD	4*REDM+4*GREM+4*BLUM ;SHADOW
	.WORD	31*REDM+31*GREM+31*BLUM ;WHITE

	.END

* EOF
