***
*** FILE: DRACT.ASM
*** ACTIVE STUFF
***
*** JFL 10 FEB 93
***

	.MLIB	"APMACS.LIB"
	.FILE	"DRACT.ASM"
	.TITLE	" <<< DERBY RACE -- ACTIVE OBJECTS >>>"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

**
** COMPILE FLAGS
**

***********************************************************************
*								         *
* 	COPYRIGHT (C) 1992 MIDWAY MANUFACTURING COMPANY.		 *
* 		       ALL RIGHTS RESERVED.				 *
*								         *
*************************************************************************

*	GET THE SYSTEM STUFF
	.INCLUDE	"AP.H"
	.INCLUDE	"DR.I"
	.INCLUDE	"DRSND.I"
	.INCLUDE	"DRO.I"
	.INCLUDE	"DRT.I"
	.INCLUDE	"DRV.I"
	
** OTHERS USE
	.DEF	ARENAPROC	;DRACT.ASM
	.DEF	ACTRANDSND	;DRACT.ASM
	.DEF	ACTRANDSNDTBL	;DRACT.ASM
	.DEF	ACTRANDICON	;DRACT.ASM
	.DEF	ACTRANDICONTBL	;DRACT.ASM
	.DEF	DROPICON	;DRACT.ASM

** WE USE
	.REF	RANDOMA2	;APUTIL.ASM
	.REF	HOLDGAME	;DRRAM.ASM
	.REF	ADDTOARENA	;DRA.ASM
	.REF	RANDOMA0	;APUTIL.ASM
	.REF	ARENAACTOBJS	;DRRAM.ASM
	.REF	MAKESND		;DRSND.ASM
	.REF	GENAOBJLIST	;DRA.ASM
	.REF	GENAOBJLISTX	;DRA.ASM
	.REF	INSOBJ		;APD.ASM	
	.REF	INSVLIST	;APD.ASM
	.REF	SETGCOL		;DR.ASM
	.REF	ICONLANDS	;DRA.ASM
	.REF	MAKESHADMULTI	;DRI.ASM

** WE USE -- RAM
	.REF	DRG		;DRRAM.ASM
	.REF	ARENATRACKINFO	;DRRAM.ASM
	.REF	OBJINITRAMAO	;DRRAM.ASM

	.TEXT
	.EVEN

**
** NOTES
**

**
** DEFINES
**

***
*** --------------------------- ARENA PROCESSES -----------------------------
***

**
** RANDOM SOUND TBL
**
** JFL 22 NOV 92
**
ACTRANDSNDTBL
	.WORD	07H		;COUNT MASK
	.WORD	SNDRAND1
	.WORD	SNDRAND2
	.WORD	SNDRAND3
	.WORD	SNDRAND4
	.WORD	SNDRAND5
	.WORD	SNDRAND6
	.WORD	SNDRAND7
	.WORD	SNDRAND8

**
** ACTRANDSND -- EXGPC A7
**
** IN & OUT
**    A2 RANDOM SOUND TBL
**    A3 AS LOADED
**    A8 AOSCRIPT
**   A14 AS LOADED
**    A0,A3,A9-A11 SCRATCH
** OUT
**    C TO NOT ADD
**
** JFL 22 NOV 92
** JFL 10 FEB 93
**
ACTRANDSND
	CALLA	RANDOMA0 ;A0 RND
	MOVW	*A2+,A14	;GET COUNTMASK
	AND	A14,A0
	SLL	4,A0		;SIZEOF RANDOM SOUND TBL
	ADD	A0,A2
	MOVW	*A2,A14
	CALLA	MAKESND ;IN:A14 SNDID
	
	SETC			;DONT ADD ANYTHING TO THE ARENA
	EXGPC	A7

**
** ICON TABLES
**
** JFL 10 FEB 93
**
ACTRANDICONTBL
	.WORD	07H			;COUNTMASK
	.WORD	AOID_ICONMISL
	.WORD	AOID_ICONMONEYBAG
	.WORD	AOID_ICONHEALTH
	.WORD	AOID_ICONTURBO
	.WORD	AOID_ICONHEALTH
	.WORD	AOID_ICONHEALTH
	.WORD	AOID_ICONMISL
	.WORD	AOID_ICONMISL

**
** ACTRANDICON -- EXGPC A7
**
** IN & OUT
**   C
**    A2 DATA -- TBL
**    A3 DATA -- UNUSED
**    A8 AOSCRIPT
**   A14 DATA -- UNUSED
**    A0-A1,A3,A9-A11 SCRATCH
** OUT
**    NC TO ADD
**    A2 [OBJID,AOLVAL16]
**    A3 [Z:X]
**   A14 [XXX1:Y]
**
** JFL 10 FEB 93
** JFL 21 FEB 93; AOLVAL16
**
ACTRANDICON
	
	;GET POSITION
	MOVL	@ARENATRACKINFO,A0
	ADDI	TIDROPTBL,A0
	MOVW	@REFCNT,A1
	SRL	2,A1
	ANDI	TIDCOUNTMASK,A1	;MAKE RND IN RANGE
	SLL	TIDSIZEOF,A1
	ADD	A1,A0
	MOVL	*A0,A3		;Z:X
	
	;CREATE THE ICON
	CALLA	DROPICON ;IN:A3 Z:X OUT:A0 OBJ SC:B0-B1,B8,A1-A5


	;SET POS & VELS
	MOVI	ICONDROPYPOS,A14
	MOVW	A14,*A0(OYPOS)
	MOVI	ICONDROPYACC,A14
	MOVL	A14,*A0(OYACC)

	SETC			;DONT ADD TO THE ARENA
	EXGPC	A7

**
** ARENAPROC
**
** JFL 10 FEB 93
**
ARENAPROC
	;WATCH THE ARENA
ARENAWATCHER
	MOVL	@ARENAACTOBJS,A8	;CHECK IF THERE IS A LIST
	JRZ	AWDIE
	MOVL	*A8,A0			;CHECK FIRST ITEM
	JRNZ	AWONE
AWDIE
	DIE
AWHOLD
	SLEEP	10
AWONE
	MOVW	@HOLDGAME,A14
	JRNZ	AWHOLD

	MOVL	*A8+,A2		;LOAD [OBJID,AOLVAL16]
	JRZ	ARENAWATCHER
	MOVL	*A8+,A3		;LOAD Z:X
	MOVL	*A8+,A14	;LOAD XXX1:Y
	
	MOVL	*A8+,A7		;FUN()
	JRZ	AWCREATE
	EXGPC	A7
	JRC	AWSLEEP		;DONT ADD
AWCREATE
	CALLA	ADDTOARENA ;A2,A3,A14 ARENAOBJID:AOLVAL16,Z:X,XXX1:Y OUT:A2 OBJ

AWSLEEP
	MOVW	*A8+,A0		;SLEEP TIME
	CALLA	PRCSLP
	JRUC	AWONE

***
*** ------------------------------------------------------------------------
***

**
** DROPICON
**
** IN
**   A3 Z:X
** OUT
**   A0 ICONOBJ 
** SCRATCH: B0-B1,B8,A1-A5
**
** JFL 24 FEB 93
** JFL 15 MAR 93
**
DROPICON
	CALLA	RANDOMA0 ;OUT:A0
	MOVE	A0,A1		;RANDOM
	MOVI	ACTRANDICONTBL,A2
	MOVW	*A2+,A14	;GET COUNTMASK
	AND	A14,A0
	SLL	4,A0		;SIZEOF TBL
	ADD	A0,A2
	MOVW	*A2,A2		;GET ICON ID
	ADDI	M_AOIDJUNK,A2	;GIVE THIS OID_JUNK SO IT WONT BE HIT TESTED
	SLL	16,A2		;OBJID:0

	;FIGURE NUMBER OF MISSILES TO GIVE AWAY
	RL	1,A1		;RANDOM
	MOVE	A1,A0
	SLL	32-4,A0
	SRL	32-2,A0		;0..3
	ADDK	2,A0		;2..5
	MOVX	A0,A2		;OBJID:AOLVAL16
	

	CALLA	ADDTOARENA ;A2,A3,A14 ARENAOBJID:AOLVAL16,Z:X,XXX1:Y OUT:A2 OBJ
	MOVE	A2,A0

	.IF DEBUG
	MOVB	*A0(OID+B_OIDFAUTOVEL-7),A14
	TLOCKON	N ;ASSUMED NOT SET OR ADDED TO VLIST
	.ENDIF ;DEBUG
	SETF	1,0,0
	MOVK	1,A14
	MOVE	A14,*A0(OID+B_OIDFAUTOVEL),0
	SETF	16,1,0
	CALLA	INSVLIST ;IN:A0,A2-A3 OBJ,SC

	MOVE	A0,B0		;SAVE
	MOVE	A8,B1		;SAVE

	;SET UP FOR WHEN IT LANDS & MAKE A SHADOW
	MOVE	A0,A8
	MOVI	ICONLANDS,A2
	MOVI	SETGCOL,B8 ;IN:A2,A8,B8 GCOL(),OBJ,PC
	EXGPC	B8

	;MAKE SHADOW
	MOVL	*A0(OIMG),A2
	CALLA	MAKESHADMULTI ;IN A2,A3-A5,A8 SHADIMG,SC,OBJ OUT:A0 SHADOBJ
	CALLA	INSOBJ ;IN:A0

	MOVE	B0,A0		;UNSAVE
	MOVE	B1,A8		;UNSAVE
	RETS

* EOF
