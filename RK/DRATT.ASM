***
*** FILE: DRATT.ASM
***
*** JFL 16 OCT 92
*** JFL 15 JAN 93
*** JFL 16 FEB 93
***
	.MLIB	"APMACS.LIB"
	.FILE	"DRATT.ASM"
	.TITLE	" <<< AMUSEMENT PARK -- ATTRACT MODE ROUTINES >>>"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

***********************************************************************
*								         *
* 	COPYRIGHT (C) 1992 MIDWAY MANUFACTURING COMPANY.		 *
* 		       ALL RIGHTS RESERVED.				 *
*								         *
*************************************************************************

	.INCLUDE	"AP.H"
	.INCLUDE	"DRB.I"
	.INCLUDE	"DRINP.I"
	.INCLUDE	"DR.I"
	.INCLUDE	"DRP.I"
	.INCLUDE	"DRPAL.I"
	.INCLUDE	"DRSND.I"
	.INCLUDE	"DRT.I"
	.INCLUDE	"DRPRO.I"
	.INCLUDE	"DRDISP.I"
	.INCLUDE	"IMGTBLB1.GLO"
	.INCLUDE	"IMGTBLM2.GLO" ;FOR TEST1 ONLY

**
** COMPILE FLAGS
**

** OTHERS USE
	.DEF	ATTRACTPROC		;DRATT.ASM
	.DEF	ATT_MODE_CREDIT		;DRATT.ASM
	.DEF	HSTD_ENTRY_POINT	;DRATT.ASM
	.DEF	STARTNEWATTRACT		;DRATT.ASM
	.DEF	STARTGUN		;DRATT.ASM
	.DEF	STARTGUNSUB		;DRATT.ASM
	.DEF	GAMEOVER	;DRATT.ASM
	.DEF	RESETBUTTONCHECK	;DRATT.ASM
	.DEF	PICKARENA		;DRATT.ASM

** WE USE
	.REF	DISPATCHPROC		;APA.ASM
	.REF	WARMSET			;AP.ASM
	.REF	WIPEOUT			;AP.ASM
	.REF	STARTPLANES		;DRB.ASM
	.REF	SCRATCH2K		;DR.ASM
	.REF	FREEALLOBJS		;APD.ASM
	.REF	SCORELIST		;APD.ASM
	.REF	INSOBJ			;APD.ASM
	.REF	ASPRSTART		;DRA.ASM
	.REF	INSANI			;APD.ASM
	.REF	FGLISTS			;APD.ASM
	.REF	ANIMP			;APC.ASM
	.REF	WINNER			;DR.ASM	
	.REF	KILL			;APPROC.ASM
	.REF	MAKESND			;DRSND.ASM
	.REF	RANDOMA2		;APUTIL.ASM
	.REF	GETINPDATA		;DRINP.ASM

	.REF	PALFXINIT	;APPALL.ASM
	.REF	PALCYCLEADD	;APPALL.ASM

	.REF	GAME_STATE
	.REF	WAVEIRQS
	.REF	IRQSKYE

** WE USE -- RAM
	.REF	DRG			;DRRAM.ASM
	.REF	SCRATCH2K		;DRRAM.ASM
	.REF	OBJINITRAM		;DRRAM.ASM
	.REF	PLAYERRECORDTBL		;DRRAM.ASM
	.REF	INDATA			;DRRAM.ASM
	.REF	PROTECTION		;DRRAM.ASM

	.TEXT
	.EVEN

**
** DEFINES
**
TIMEFORDRONEPLAY	EQU	ONESECOND*10

**
** CREATEANIM
**
** IN
**   A2 ANIMSCRIPT
**   A3 SCRATCH
** OUT
**   A0 OBJ
**
** JFL 17 FEB 93
**
CREATEANIM
	MOVE	A5,A3			;SAVE
	MOVI	OBJINITRAM,A5
	MOVL	*A2,*A5			;SET IHDR IN CREATE OBJ BLOCK
	CALLA	CREATE_OBJ ;IN:A5 COBLOCK OUT:A0 OBJ
	TLOCKON	Z
	MOVE	A3,A5			;UNSAVE
	CALLA	INSOBJ
	MOVL	A2,*A0(ANIMFRM)
	MOVL	A2,*A0(ANIMSCR)
	MOVK	1,A14
	MOVW	A14,*A0(ANIMSLPB)
	CALLA	INSANI
	RETS

**
** PICKARENA
**
** JFL 10 DEC 92
**
PICKARENA
	;SET ARENA FOR NEXT WAVE
	MOVI	DRG,A2
	MOVW	*A2(DRGARENA),A14
	JRN	PAOUTOFORDER
	DEC	A14
	JRNN	PAOK
PALAST
	MOVI	ADIDARENAX-1,A14
PAOK
	.IF 0 ;PROTECT
	MOVI	PROTECTION-180H,A3
	MOVW	*A3(PROREFCNT0+180H),A3
	SUBI	VPROREFCNT0-VPROREFCNT0SLUFF,A3
	JRN	PAPROBAD	      	;TOO SMALL
	SUBK	2*VPROREFCNT0SLUFF,A3
	JRN	PAPRODONE		;OK
PAPROBAD
	;--- NOT NOW TLOCKUP ;PROTECTION TRIPPED
	CLR	A14			;ALWAYS PICK FIRST ARENA
PAPRODONE
	.ENDIF ;PROTECT

	MOVK	5,A14 ;LOCK INTO AR6
	MOVW	A14,*A2(DRGARENA)
	RETS
PAOUTOFORDER
	SLL	25,A14
	SRL	25,A14
	CMPI	ADIDARENAX-1,A14
	JRHI	PALAST
	JRUC	PAOK

OBJINITTESTPATTERN
	.LONG	TEST1		;IMAGE
	.LONG	0		;COLLISION VECTOR
 	.WORD	OID_JUNK  	;OID
	.WORD	DMAWNZ		;OCTRL
	.WORD	0		;OFLAGS
	.WORD	STYPNONE	;OSCALTYP
 	.LONG	0		;CFUNC
	
	.LONG	[0,0]		;Y:X

MAKETESTOBJ
	CALLA	CREATE_OBJ
	JRZ	MTOX
	MOVW	A2,*A0(OXPOS)
	RL	16,A2
	MOVW	A2,*A0(OZPOS)
	CALLR	INSOBJ ;IN:A0
	RL	16,A2
	MOVE	A0,A0		;SET NZ
MTOX
	RETS

**
** TESTMODE
**
** JFL 09 MAR 93
**
TESTMODE

	DISPOFF
	SLEEP	1			;WAIT FOR DISPLAY TO CATCH UP

	MOVI	INATTMODE,A14
	MOVW	A14,@GAME_STATE

	CALLA	WIPEOUT ;INIT FOR NEW WAVE

	;SET ERASE COLOR
	MOVI	TITLEERASE,A14
	MOVW	A14,@IRQSKYE

	CLR	A2			;Y:X
       	MOVK	4,A4

TMLOOP0
	MOVK	4,A3

TMLOOP1
	;CREATE TEST PATTERN
	MOVI	OBJINITTESTPATTERN,A5
	CALLR	MAKETESTOBJ ;IN:A2,A5
	JRZ	TMX
	
	MOVW	*A0(OUSIZEX),A14
	ADDXY	A14,A2
	
	DSJ	A3,TMLOOP1

	CLR	A14
	MOVX	A14,A2

	MOVW	*A0(OUSIZEY),A14
	SLL	16,A14
	ADDXY	A14,A2

	DSJ	A4,TMLOOP0



	DISPON

	SLEEP	50

TMLOOP
	SLEEP	2
	CALLR	CREDITCHECK ;IN A0-A3 SC OUT: NZ IF CREDIT
	JRZ	TMLOOP
TMX
	JRUC	AMMAIN


OBJINITSELBOX
	.LONG	SEL_BOX		;IMAGE
	.LONG	0		;COLLISION VECTOR
 	.WORD	OID_JUNK  	;OID
	.WORD	DMAWNZ		;OCTRL
	.WORD	0		;OFLAGS
	.WORD	STYPNONE	;OSCALTYP
 	.LONG	0		;CFUNC

**
** ATTRACTPROC
**
** JFL 16 OCT 92; FROM DREDD
** JFL 16 FEB 93
**
ATTRACTPROC
GAMEOVER
WIN_MODE
AMMAIN
	DISPOFF
	SLEEP	1			;WAIT FOR DISPLAY TO CATCH UP

	MOVI	INATTMODE,A14
	MOVW	A14,@GAME_STATE

	CALLR	RESETBUTTONCHECK ;SC:A1-A3
	CALLA	WIPEOUT ;INIT FOR NEW WAVE

	MOVK	PLIDTITLE,A2
	CALLR	STARTPLANES ;IN: A2 PLANEID

	;SET ERASE COLOR
	MOVI	TITLEERASE,A14
	MOVW	A14,@IRQSKYE

	;START A SONG
	MOVI	SNDSONGTHRASHROCK,A14
	CALLA	MAKESND ;IN:A14 SNDID

	CREATE	PID_IND,ANIMP

	DISPON

	;TMP -------------

	CALLA	PALFXINIT

	MOVI	OBJINITSELBOX,A5
	CALLA	CREATE_OBJ
	MOVI	[SCRBOT/2,0],A14
	MOVL	A14,*A0(OZVAL)
	MOVI	[SCRRGT/2,0],A14
	MOVL	A14,*A0(OXVAL)
	CALLR	INSOBJ ;IN:A0

	MOVI	[0051FH,0001],A2
	MOVW	*A0(OPAL),A14
	SRL	8,A14
	SLL	8,A14
	OR	A14,A2
	MOVK	2,A3
	CALLA	PALCYCLEADD ;IN:A2,A3 VEL.8:COUNT.8:PAL.8:COL.8,TIME

AMLOOPXX
	SLEEP	2
	CALLR	CREDITCHECK ;IN:A0-A3 SC OUT: NZ IF CREDIT
	JRZ	AMLOOPXX

	;TMP -------------


	;PRESS START ANIM
	MOVI	ASPRSTART,A2
	CALLR	CREATEANIM ;IN:A2,A3 AS,SC OUT:A0 OBJ
	MOVI	PRSTARTX,A14
	MOVW	A14,*A0(OXPOS)
	MOVI	PRSTARTY,A14
	MOVW	A14,*A0(OZPOS)

	SLEEP	50

	MOVI	60,A8
AMLOOP0
	SLEEP	2
	CALLR	CREDITCHECK ;IN A0-A3 SC OUT: NZ IF CREDIT
	JRNZ	ATT_MODE_CREDIT
	DSJ	A8,AMLOOP0

AMLOOP
	SLEEP	2
	CALLR	CREDITCHECK ;IN:A0-A3 SC OUT: NZ IF CREDIT
	JRNZ	ATT_MODE_CREDIT

	;CHECK IF WE TIME OUT
	MOVL	@WAVEIRQS,A14
	CMPI	TIMEFORDRONEPLAY,A14
	JRHS	HSTD_ENTRY_POINT

	JRUC	AMLOOP


ATT_MODE_CREDIT
HSTD_ENTRY_POINT

	CALLR	PICKARENA
	
	CREATE	PID_MAIN,DISPATCHPROC
	DIE

**
** RESETBUTTONCHECK
**
** SC: A1-A3
**
** JFL 24 MAR 93
**
RESETBUTTONCHECK

	MOVK	DRPLAYERS,A3
	MOVI	INDATA,A2
	MOVI	PLAYERRECORDTBL,A1
RBCLOOP
	MOVW	*A2(INPBSTART),*A1(PLRBUTTON)
	ADDI	PLRSIZE,A1
	ADDI	INPSIZE,A2
	DSJ	A3,RBCLOOP

	RETS

**
** CREDITCHECK
**
** IN
**   A0-A3 SCRATCH
** OUT
**   NZ IF CREDIT
**
** JFL 17 FEB 93
** JFL 24 MAR 93
**
CREDITCHECK

	;
	;CHECK FOR EXISTING CREDIT
	;
	MOVK	DRPLAYERS,A3
   	MOVI	PLAYERRECORDTBL,A2
CCLOOP1
	MOVW	*A2(PLRCREDITS),A14
	JRNZ	CCX
	ADDI	PLRSIZE,A2
	DSJ	A3,CCLOOP1

	;
	;CHECK FOR BUTTON PRESS
	;
	MOVK	DRPLAYERS,A3
	MOVI	INDATA,A2
   	MOVI	PLAYERRECORDTBL,A1
CCLOOP2
	MOVW	*A2(INPBSTART),A14
	MOVW	*A1(PLRBUTTON),A0
	CMP	A0,A14
	JRNE	CCX
	ADDI	PLRSIZE,A1
	ADDI	INPSIZE,A2
	DSJ	A3,CCLOOP2
	
	CLR	A14			;SET Z
	
CCX
	RETS

**
** STARTNEWATTRACT
**
** JFL 16 NOV 92
** JFL 24 JAN 93; PUSHST & POPST
**
STARTNEWATTRACT
	PUSHST
	DINT
	DISPOFF

	;CLEAR OLD OBJS
	CALLA	FREEALLOBJS ;IN:A0 LIST
	CALLA	STARTPLANES ;IN: A2 PLANEID

	DISPON
	POPST

	RETS


***
*** START GUN --------------------------------------------------------------
***


SGSND	EQU	20H
SGDELAY	EQU	30H

SGCOUNT
	.LONG	FIVE
	.WORD	0
	.WORD	40
	.LONG	FOUR
	.WORD	0
	.WORD	40
	.LONG	THREE
	.WORD	0
	.WORD	40
	.LONG	TWO
	.WORD	0
	.WORD	40
	.LONG	ONE
	.WORD	0
	.WORD	40
SGRSG
	.LONG	R_1
	.WORD	SNDKILLMISL1
	.WORD	40
	.LONG	S_2
	.WORD	SNDKILLMISL1
	.WORD	40
	.LONG	G_3
	.WORD	SNDKILLMISL1
	.WORD	10
       	.LONG	0
	.WORD	0
	.WORD	0

OBJINITSG
	.LONG	FIVE		;IMAGE
	.LONG	0		;COLLISION VECTOR
 	.WORD	OID_JUNK  	;OID
	.WORD	DMAWNZ		;OCTRL
	.WORD	0		;OFLAGS
	.WORD	STYPNONE	;OSCALTYP
 	.LONG	0		;CFUNC
	;
	.LONG	[100,200]	;Y:X

	.BSS	SGA,32
	.BSS	SGTIME,32

SGZOFF	EQU	200

**
** IN
**   A1 NEW IHDR
**   A8 OBJ
**
SGSWAPOBJ
	move	A1,*A8(OIMG),L
	move	*A1(ISIZE),A14,L	;ISIZE (offset 0)
	move	A14,*A8(OSIZE),L
	MOVE	A14,*A8(OUSIZE),L
	move	*A1(ISAG),*A8(OSAG),L
	MOVW	*A8(OCTRL),A2
	MOVE	*A1(ICTRL),A14
	ANDI	0807FH,A2
	OR	A2,A14	
	MOVW	A14,*A8(OCTRL)

	MOVE	*A1(IANIOFF),A14,L
	MOVE	A14,*A8(OANIOFF),L
	move	A14,*A8(OUANIOFF),L

	CLR	A14
	MOVW	A14,*A8(OSCALEMUL)
	RETS
	
	.BSS	SGPLBTN,16*3


**
** STARTGUN
**
STARTGUN
	
	CLR	A10
	MOVL	A10,@SGTIME
	
	MOVI	OBJINITSG,A5
	CALLA	CREATE_OBJ
	TLOCKON	Z
	MOVL	A0,@SGA
	MOVE	A0,A8

	MOVL	*A5+,A14
	MOVW 	A14,*A0(OXPOS)
	SRL	16,A14
	MOVI	SGZOFF,A2
	ADD	A2,A14
	MOVW	A14,*A0(OZPOS)
	NEG	A2
	MOVW	A2,*A0(OZOFF)

	CALLR	INSOBJ

	;SET OBJ
	MOVE	A10,A14
	SLL	6,A14
	ADDI	SGCOUNT,A14
	MOVL	*A14,A1
	CALLR	SGSWAPOBJ ;IN:A1,A8 IHDR,OBJ

	;CLEAR IT TO START WITH
	CLR	A1
	CLR	A2
SGILOOP
	CALLA	GETINPDATA ;IN:A2 PLNUM OUT:A2,A3 PLNUM,INPDATA
	MOVW	*A3(INPB2),A1	;GET BUTTON
	
	MOVE	A2,A14
	SLL	4,A14
	ADDI	SGPLBTN,A14
	MOVW	A1,*A14		;SAVE BUTTON FOR US

	INC	A2
	CMPI	3,A2
	JRLT	SGILOOP

	MOVE	A10,A14
	SLL	6,A14
	ADDI	SGCOUNT,A14
	MOVW	*A14(SGDELAY),A9
	MOVW	*A14(SGSND),A14
	CALLA	MAKESND ;IN:A14 SND

	RETS


**
** STARTGUNSUB
**
** IN
**    A8 OBJ
**    A9 TIME FOR CHANGE
**   A10 DECADE COUNT
**   A11
**
** JFL 05 FEB 93
**
STARTGUNSUB

	MOVL	@SGTIME,A14	
	INC	A14
	MOVL	A14,@SGTIME

	;CLEAR IT TO START WITH
	CLR	A2
SGLOOP
	CALLA	GETINPDATA ;IN:A2 PLNUM OUT:A2,A3 PLNUM,INPDATA
	MOVW	*A3(INPB2),A1	;GET BUTTON

	;LOOK UP BTN COUNT & WAIT FOR PLAYER TO PUSH IT DOWN
	MOVE	A2,A3
	SLL	4,A3
	ADDI	SGPLBTN,A3
	MOVW	*A3,A14		;GET OUR COPY OF BUTTON
	CMP	A14,A1
	JREQ	SGNEXTBTN      	;BTN HAS NOT CHANGED
	MOVW	A1,*A3		;SAVE NEW STATE
	SRL	1,A1		;CHECK IF BTN IS DOWN
	JRC	SGNEXTPL	;BUTTON IS DOWN

SGNEXTBTN
	;MOVE ONTO NEXT PLAYERS BUTTONS
	INC	A2
	CMPI	3,A2
	JRLT	SGLOOP
	JRUC	SGNEXT

SGNEXTPL			     
	;SPEED UP DUE TO BTN PRESS
	MOVL	@SGTIME,A14	
	JRUC	SGNFAST

SGNEXT
	;TIME TO CHANGE?
	MOVL	@SGTIME,A14	
	CMP	A9,A14
	JRLO	SGX

SGNFAST
	;SET UP FOR NEXT CHANGE
	MOVE	A14,A9

	MOVE	A10,A14
	SLL	6,A14
	ADDI	SGCOUNT,A14
	MOVW	*A14(SGDELAY),A2
	ADD	A2,A9

	;SET OBJ
	INC	A10
	MOVE	A10,A14
	SLL	6,A14
	ADDI	SGCOUNT,A14
	MOVL	*A14,A1
	JRZ	SGZAP
	CALLR	SGSWAPOBJ ;IN:A1,A8 IHDR,OBJ
	
	MOVE	A10,A14
	SLL	6,A14
	ADDI	SGCOUNT,A14
	MOVW	*A14(SGSND),A14
	CALLA	MAKESND ;IN:A14 SND

SGX
	;NOT DONE
	CLRC
	RETS
SGZAP
	;DONE


	MOVL	@SGA,A8
	CALLA	ZAP_OBJ
	SETC
	RETS

***
*** -----------------------------------------------------------------------
***

PRSTARTX	EQU	200
PRSTARTY	EQU	228

	.END


* EOF
